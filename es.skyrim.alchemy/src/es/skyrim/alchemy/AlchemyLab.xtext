grammar es.skyrim.alchemy.AlchemyLab hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate alchemyLab "http://www.skyrim.es/alchemy/AlchemyLab"

AlchemyLabModel:
	effects += EffectDef*
	ingredients += IngredientDef*
	recipes += Recipe*;
	

Effect: EffectDef | EffectAlias;

EffectDef:
	'effect' name=ID '(' type=EffectType ')' (':' description=STRING)?
;


enum EffectType:
	FORTIFY='+' | WEAKEN='-';


EffectRef returns Effect : '->' effect=[Effect];

EffectAlias: 'ealias' alias=ID 'for' effect=[EffectDef];

Ingredient: IngredientDef | IngredientAlias;

IngredientDef:
	'ingredient' name=ID '{'
		effects+=EffectRef* 
		
		'price:' price = NUMBER
		'weight:' weight = NUMBER 
		('source:' source = STRING)?
	'}'
;


IngredientRef returns Ingredient: ingredient=[Ingredient];

IngredientAlias: 'ialias' alias=ID 'for' ingredient=[IngredientDef];

Recipe:
	toxicity=Toxicity name=ID '{'
		effects+=EffectRef+
	
		('price:' price = NUMBER)?

	'}' '=' ingredients+=IngredientRef ('+' ingredients+=IngredientRef)*
;


enum Toxicity:
	POTION='potion' | POISON='poison' | MIXTURE='mixture';



terminal ID : '"' '^'?('a'..'z'|'A'..'Z'|' '|'ä'|'Ä'|'ö'|'Ö'|'Ü'|'ü'|'ß'|"'")+ '"'; 

terminal STRING : "'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'";

terminal NUMBER returns ecore::EFloat:
	('0'..'9')+ ('.' ('0'..'9')+)?;
	
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;

